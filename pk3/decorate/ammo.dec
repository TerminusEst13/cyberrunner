actor ForceVentAmmo : Ammo 15401
{
    Inventory.Amount 3
    Inventory.MaxAmount 3
    Ammo.BackpackMaxAmount 3

    +INVENTORY.IGNORESKILL
    +FLOATBOB
    +NOGRAVITY

    Inventory.PickupMessage "$CYBERRUNNER_PK_VENT"
    Inventory.PickupSound "sounds/vent_pickup.ogg"

    RenderStyle Add
    Alpha 0.75

    States
    {
      Spawn:
        VENT Z 2 bright A_SpawnItemEx("ForceVentSparkle", random(8, 24),0,random(16, 24), 0,0,random(2,5)/5.0, random(0,360))
        loop
    }
}

actor ForceVentSparkle
{
    +NOINTERACTION
    +CLIENTSIDEONLY

    ReactionTime 24

    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 0 A_SpawnItemEx("ForceVentSparkle2")
        TNT1 A 1 A_Countdown
        loop

      Death:
        TNT1 A 0
        stop
    }
}

actor ForceVentSparkle2
{
    +NOINTERACTION
    +CLIENTSIDEONLY

    RenderStyle Add
    Alpha 0.10
    Scale 0.75

    States
    {
      Spawn:
        VENT Y 1 bright A_FadeOut(0.01)
        loop
    }
}

actor CyberAllAmmo: CustomInventory 15452
{
    +FLOATBOB
    +NOGRAVITY
    +AUTOACTIVATE

    Inventory.MaxAmount 0
    Inventory.PickupMessage "$CYBERRUNNER_PK_ALLAMMO"

    RenderStyle Add
    Alpha 0.9

    States
    {
      Spawn:
        AMMO Z 2 bright A_SpawnItemEx("AllAmmoSparkle", random(8, 24),0,random(16, 24), 0,0,random(2,5)/5.0, random(0,360))
        loop

      Pickup:
        TNT1 A 0 A_SetBlend("FF FF FF", 0.25, 8)
        TNT1 A 0 A_GiveInventory("PlasmaGunAmmo", 100)
        TNT1 A 0 A_GiveInventory("ShotgunAmmo", 20)
        TNT1 A 0 A_GiveInventory("CarbineAmmo", 10)
        stop
    }
}

actor AllAmmoSparkle: RandomSpawner
{
    +CLIENTSIDEONLY

    DropItem "CyberPlasmaSparkle"
    DropItem "CyberShotgunSparkle"
    DropItem "CyberCarbineSparkle"
    DropItem "CyberMinigunSparkle"
}
