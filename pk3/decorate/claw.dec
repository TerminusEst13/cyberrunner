actor GrappleTIDIndicator: Inventory
{
  +UNDROPPABLE
  +INTERHUBSTRIP
  Inventory.MaxAmount 0x7FFFFFFF
}

actor GrappleHit: Boolean {}

actor GrappleFromInv: Inventory
{
  +UNDROPPABLE
  +INTERHUBSTRIP
  Inventory.MaxAmount 1
}


actor GrapplingShot
{
  Projectile
  Scale 1.5
  SeeSound ""
  Speed 128
  Radius 4
  Height 4
  RenderStyle Add
  States
  {
    Spawn:
      NDPL A 0
      NDPL A 0 ACS_ExecuteAlways(654, 0, 2,0,0)
      goto SpawnLoop

    SpawnLoop:
      //PLSS AB 6 bright
      NDPL AB 6 bright
      loop

    Death:
      TNT1 A 1
      stop
  }
}


actor GrapplePuff
{
  Radius 0
  Height 0
  +CLIENTSIDEONLY
  +NOGRAVITY
  +NOTARGET
  +NOCLIP
  +PUFFGETSOWNER
  -ALWAYSPUFF
  +PUFFONACTORS
  States
  {
    Spawn:
      TNT1 A 0
      TNT1 A 0
	  TNT1 A 0 A_GiveToTarget("GrappleHit")
	  TNT1 AAAAA 0 A_SpawnItemEx("GrappleSparks",0,0,0,random(-200,200)/100.00,random(-200,200)/100.00,random(-200,200)/100.00,0,160)
	  TNT1 AAAAA 0 A_SpawnItemEx("GrappleSparks",0,0,0,random(-200,200)/100.00,random(-200,200)/100.00,random(-200,200)/100.00,0,160)
	  TNT1 AAAAA 0 A_SpawnItemEx("GrappleSparks",0,0,0,random(-200,200)/100.00,random(-200,200)/100.00,random(-200,200)/100.00,0,160)
	  TNT1 AAAAA 0 A_SpawnItemEx("GrappleSparks",0,0,0,random(-200,200)/100.00,random(-200,200)/100.00,random(-200,200)/100.00,0,160)
      NDPL A 0 A_TakeFromTarget("GrappleTIDIndicator")
      NDPL A 0 A_GiveToTarget("GrappleTIDIndicator", ACS_ExecuteWithResult(657, 2,0,0) )
	  TNT1 A 1
	  TNT1 A 0 A_PlaySound("claw/hit")
      TNT1 A -1 // stay until Thing_Removed
      stop
  }
}

actor GuaranteedTeleportFog: TeleportFog
{
  +NOINTERACTION
  Radius 0
  Height 0
}

actor GrappleSpot
{
  +NOINTERACTION
  Radius 0
  Height 0
  States
  {
    Spawn:
      TNT1 A -1 // stay here until Thing_Removed
      stop
  }
}

actor GrappleSparks
{
  +NOINTERACTION
  +CLIENTSIDEONLY
  Radius 0
  Height 0
  RenderStyle Add
  Alpha 0.95
  Scale 0.1
  States
  {
    Spawn:
      RBPL A 2 bright A_FadeOut(0.05)
      loop
  }
}

actor GrapplePoint
{
  +NOINTERACTION
  +CLIENTSIDEONLY
  Radius 0
  Height 0
  RenderStyle Add
  Alpha 0.55
  Scale 0.45
  States
  {
    Spawn:
      RBPL A 2 bright
      stop
  }
}

actor CyberClawAmmo : Ammo
{
    Inventory.MaxAmount 5
    Ammo.BackpackMaxAmount 5
    +IGNORESKILL
    +UNDROPPABLE
}

actor "Cyber Claw": CyberrunnerWeapon
{
    Weapon.AmmoType "CyberClawAmmo"
    Weapon.AmmoUse 0
    Weapon.AmmoType2 "ForceVentAmmo"
    Weapon.AmmoUse2 1
	Weapon.UpSound "claw/raise"
    Weapon.SelectionOrder 10000
    +WIMPY_WEAPON
    +NOALERT
	+AMMO_OPTIONAL
    Obituary "%k \cgmolested \c-%o \cgto death. Somehow.\c-"
    Inventory.PickupMessage "$CYBERRUNNER_PK_CLAW"
    Weapon.SlotNumber 0
    States
    {
    Ready:
      TNT1 A 1 A_WeaponReady
      loop

    Fire:
      TNT1 A 0
      TNT1 A 0 A_JumpIfInventory("CyberClawAmmo",1,1)
	  goto DryFire
	  TNT1 A 0
      TNT1 A 2 A_FireBullets(0, 0, 1, 0, "GrapplePuff", 32767, 4072)
	  TNT1 A 0 A_JumpIfInventory("GrappleHit",1,"GrappleSound")
	  TNT1 A 15 A_PlayWeaponSound("claw/firemiss")
	  goto Ready
    GrappleSound:
	  TNT1 A 0 A_PlayWeaponSound("claw/firehit")
      TNT1 A 0 ACS_ExecuteAlways(655, 0, ACS_ExecuteWithResult(657, 1), 2, 0)
      TNT1 A 0 A_TakeInventory("GrappleTIDIndicator")
      TNT1 A 0 A_TakeInventory("GrappleHit")
	  TNT1 A 0 A_TakeInventory("CyberClawAmmo",1)
    Hold:
      TNT1 A 1
      TNT1 A 0 A_Refire
	  TNT1 A 1
	  TNT1 A 20 A_PlaySoundEx("claw/fireend","soundslot6",0,0)
      goto Ready
    }
}