actor ShotgunAmmo: Ammo
{
    Inventory.MaxAmount 20
    Ammo.BackpackMaxAmount 20
    +IGNORESKILL
    +UNDROPPABLE
}

actor ShotgunAmmoPickup : ShotgunAmmo replaces ShellBox
{
    +NOGRAVITY
    +FLOATBOB
    RenderStyle Add
    Alpha 0.95
    Inventory.PickupSound "pickups/amsmal"
    Inventory.PickupMessage "$CYBERRUNNER_PK_SHOTGUNAM"
    Inventory.Amount 8
    States
    {
    Spawn:
      AMSH A -1
      Stop
    }
}

actor "Cyber Shotgun": CyberrunnerWeapon
{
    Weapon.AmmoType1 "ShotgunAmmo"
    Weapon.AmmoGive1 20
    Weapon.AmmoUse1 1
    Weapon.AmmoType2 "ForceVentAmmo"
    Weapon.AmmoUse2 1
    
    Weapon.SlotNumber 2
    Weapon.UpSound "shotgun/up"
    
    Inventory.PickupMessage "$CYBERRUNNER_PK_SHOTGUN"
    Inventory.PickupSound "shotgun/pickup"
    Obituary "$CYBERRUNNER_OB_SHOTGUN"
    
    States
    {
      Spawn:
        CYSG Z 2 bright
        loop

      Fire:
        TNT1 A 0
        TNT1 A 0 A_JumpIfInventory("WallHackVision",1,"DryFire")
        TNT1 A 0 A_JumpIfNoAmmo("DryFire")
        TNT1 A 0 A_PlaySound("shotgun/fire", CHAN_WEAPON)
        TNT1 A 0 A_FireBullets(4, 3, 10, 8, "ShotgunPuff", FBF_USEAMMO | FBF_NORANDOM)
        TNT1 A 0 A_FireBullets(4, 3, 15, 6, "ShotgunPuffNoTracer", FBF_NORANDOM)
        TNT1 A 20
        TNT1 A 10 A_PlaySound("shotgun/load", 5)
        TNT1 A 10 A_WeaponReady(12)
        goto Ready
    }
}

actor CyberShotgunSparkle
{
    +NOINTERACTION
    +CLIENTSIDEONLY
    
    ReactionTime 18
    
    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 0 A_SpawnItemEx("CyberShotgunSparkle2")
        TNT1 A 1 A_Countdown
        loop
        
      Death:
        TNT1 A 0
        stop
    }
}

actor CyberShotgunSparkle2
{
    +NOINTERACTION
    +CLIENTSIDEONLY
    
    RenderStyle Add
    Alpha 0.75
    Scale 0.75
    
    States
    {
      Spawn:
        CYSG Y 1 bright A_FadeOut(0.05)
        loop
    }
}

actor ShotgunPuff
{
    +NOINTERACTION
    +ALWAYSPUFF
    +PUFFONACTORS
    +PUFFGETSOWNER
    +NOTIMEFREEZE
    +SERVERSIDEONLY
    
    Decal BulletChip
    AttackSound "shotgun/hit"
    States
    {
      Spawn:
        TNT1 A 1
		TNT1 A 0 A_GiveToTarget("CyberHitIndicator")
        TNT1 A 0 ACS_ExecuteWithResult(412)
        goto AfterTracer
		
	  Crash:
	    TNT1 A 1
		TNT1 A 0 ACS_ExecuteWithResult(412)
		goto AfterTracer
        
      AfterTracer:
        TNT1 AA 0 A_SpawnItemEx("ShotgunSpark", 0,0,0, random(0,60)/10.0, 0, random(0,60)/10.0, random(0,360))
        TNT1 A 70
        stop
    }
}

actor ShotgunPuffNoTracer: ShotgunPuff
{
    States
    {
      Spawn:
        TNT1 A 0
        goto AfterTracer
    }
}

actor ShotgunTracerPoint
{
    +NOINTERACTION
    +CLIENTSIDEONLY
    +PUFFGETSOWNER
    +NOTIMEFREEZE
    
    Radius 0
    Height 0
    
    States
    {
      Spawn:
        BAL1 A 35
        stop
    }
}

actor ShotgunTracerTID: Inventory
{
    Inventory.MaxAmount 0x7FFFFFFF
    +UNDROPPABLE
}

actor ShotgunTID: Inventory
{
    Inventory.MaxAmount 0x7FFFFFFF
    +UNDROPPABLE
}

actor DrawShotgunTracer: CustomInventory
{
    +AUTOACTIVATE
    Inventory.MaxAmount 0
    
    States
    {
      Pickup:
        TNT1 A 0 ACS_ExecuteWithResult(412, 2)
        stop
    }
}

actor ShotgunSpark
{
    //+NOCLIP
    +NOTELEPORT
    +CLIENTSIDEONLY
    
    Projectile
    -NOGRAVITY
    +DOOMBOUNCE
    
    Radius 2
    Height 2
    
    BounceFactor 0.3
    WallBounceFactor 0.25
	BounceCount 4
    
    RenderStyle Add
    Alpha 0.75
    Scale 0.35
    
    States
    {
      Spawn:
        PUFF A 4 bright
        PUFF A 66 bright //A_ChangeFlag("NOCLIP", 0)
        goto Death
        
      Death:
		TNT1 A 0 A_Stop
        PUFF A 1 A_FadeOut(0.05)
        wait
    }
}

actor ShotgunTracer
{
    +NOINTERACTION
    +CLIENTSIDEONLY
    +NOTIMEFREEZE
    
    Radius 1
    Height 1
    
    RenderStyle Add
    Alpha 0.75
    Scale 0.35
    
    States
    {
      Spawn:
        PUFF A 1 A_FadeOut(0.1)
        wait
    }
}
