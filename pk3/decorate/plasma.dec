actor "Plasma Gun" : CyberrunnerWeapon
{
    Weapon.AmmoType1 "PlasmaGunAmmo"
    Weapon.AmmoUse1 1
    Weapon.AmmoType2 "ForceVentAmmo"
    Weapon.AmmoUse2 1
    Weapon.SlotNumber 1
	Weapon.UpSound "plasma/up"

    Inventory.PickupMessage "$CYBERRUNNER_PK_PLASMA"

    States
    {
      Fire:
	    TNT1 A 0 A_JumpIfInventory("WallHackVision",1,"DryFire")
        TNT1 A 0 A_JumpIfNoAmmo("DryFire")
        TNT1 A 0 A_PlayWeaponSound("plasma/shot")
        TNT1 A 0 A_FireCustomMissile("PlasmaBolt",0,1,0,1)
        TNT1 AAAAA 1 A_JumpIfInventory("RightMouseTapped", 1, "AltFire")
        goto Ready
    }
}

actor PlasmaGunAmmo : Ammo
{
    Inventory.MaxAmount 100
    Ammo.BackpackMaxAmount 100
    +INVENTORY.IGNORESKILL
}

actor "No-Damage Plasma Gun": "Plasma Gun"
{
    Weapon.AmmoType1 RedPlasmaGunAmmo

    States
    {
      Fire:
	    TNT1 A 0 A_JumpIfInventory("WallHackVision",1,"DryFire")
        TNT1 A 0 A_JumpIfNoAmmo("DryFire")
        TNT1 A 0 A_PlayWeaponSound("plasma/shot")
        TNT1 A 0 A_FireCustomMissile("RedPlasmaBolt",0,1,0,1)
        TNT1 A 5
        goto Ready
    }
}

actor RedPlasmaGunAmmo : Ammo
{
    Inventory.MaxAmount 100
    Ammo.BackpackMaxAmount 100
    +INVENTORY.IGNORESKILL
}

actor PlasmaBolt: FastProjectile
{
    Radius 5
    Height 6

    Projectile
    Speed 150
    Damage (15)

    Scale 0.75
    RenderStyle Add
    Alpha 0.85

    DeathSound "plasma/hit"
    Decal PlasmaScorch
    Obituary "$CYBERRUNNER_OB_PLASMA"

    +NODAMAGETHRUST

    States
    {
      Spawn:
        XLSS AABBCCDDEEFF 1 BRIGHT A_CustomMissile("PlasmaTrail",0,0,0)
        Loop

      Death:
        XLSE A 0 A_CustomMissile("PlasmaClimbPush", 0,0,0, CMF_TRACKOWNER, 0)
        XLSE A 2 BRIGHT A_Scream
        //XLSE AAAAAAAAAAAA 0 A_CustomMissile("PlasmaSpark2",0,0,Random(-180,180),50,Random(-180,180))
        XLSE BCDEF 2 BRIGHT 
        Stop
    }
}

actor RedPlasmaBolt: PlasmaBolt
{
    Translation "192:207=16:47"
    Decal RedPlasmaScorch
    Damage (0)

    States
    {
      Spawn:
        XLSS AABBCCDDEEFF 1 BRIGHT A_CustomMissile("RedPlasmaTrail",0,0,0)
        Loop

      Death:
        XLSE A 0 A_CustomMissile("PlasmaClimbPush", 0,0,0, CMF_TRACKOWNER, 0)
        XLSE A 2 BRIGHT A_Scream
        //XLSE AAAAAAAAAAAA 0 A_CustomMissile("RedPlasmaSpark2",0,0,Random(-180,180),50,Random(-180,180))
        XLSE BCDEF 2 BRIGHT 
        Stop
    }
}

actor PlasmaClimbPush
{
    Radius 1
    Height 1
    Speed 0
    Projectile
    Damage 0
    DeathSound "force/hit"
	DamageType "NoDamage"
    RenderStyle Add
    Alpha 0.85
    Decal Scorch
    States
    {
      Spawn:
        TNT1 A 0
        goto Death

      Death:
        TNT1 A 1 A_Explode(24, 48)
        stop
    }
}

actor PlasmaTrail
{ 
    Alpha 0.4
    Scale 0.6

    +NOINTERACTION
    +CLIENTSIDEONLY
    RenderStyle Add

    States
    {
      Spawn:
        XLSS ABC 1 BRIGHT A_CustomMissile("PlasmaSpark",0,0,Random(-180,180),2,Random(-100,100))
        XLSS DEF 1 BRIGHT
        Stop
    }
}

actor RedPlasmaTrail: PlasmaTrail
{
    Translation "192:207=16:47"

    States
    {
      Spawn:
        XLSS ABC 1 BRIGHT A_CustomMissile("RedPlasmaSpark",0,0,Random(-180,180),2,Random(-100,100))
        XLSS DEF 1 BRIGHT
        Stop
    }
}

actor PlasmaSpark
{
    Scale 0.065
    Gravity 0.01
    Speed 1
    Renderstyle Add
    PROJECTILE
    +NOTELEPORT
    +EXPLODEONWATER
    -CANBOUNCEWATER
    +DONTSPLASH
    -SOLID
    -NOGRAVITY
    +DOOMBOUNCE
    +CLIENTSIDEONLY
    States
    {
      Spawn:
        XLSS ABCDEF 1 BRIGHT A_FadeOut(0.03)
        loop
    }
}

actor RedPlasmaSpark: PlasmaSpark
{
    Translation "192:207=16:47"
}

actor PlasmaSpark2 : PlasmaSpark
{
    Scale 0.4
    States
    {
      Spawn:
        XLSS ABCDEF 1 BRIGHT A_FadeOut(0.1)
        loop
    }
}

actor RedPlasmaSpark2: PlasmaSpark2
{
    Translation "192:207=16:47"
}

actor CyberPlasmaSparkle
{
    +NOINTERACTION
    +CLIENTSIDEONLY
    
    ReactionTime 18
    
    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 0 A_SpawnItemEx("CyberPlasmaSparkle2")
        TNT1 A 1 A_Countdown
        loop
        
      Death:
        TNT1 A 0
        stop
    }
}

actor CyberPlasmaSparkle2
{
    +NOINTERACTION
    +CLIENTSIDEONLY
    
    RenderStyle Add
    Alpha 0.75
    Scale 0.5
    
    States
    {
      Spawn:
        CYPL Y 1 bright A_FadeOut(0.05)
        loop
    }
}
