actor CannonAmmo: Ammo
{
    Inventory.MaxAmount 15
    Ammo.BackpackMaxAmount 15
    +IGNORESKILL
}

actor "Celestial Hammer": CyberrunnerWeapon
{
    Weapon.AmmoType1 "CannonAmmo"
    Weapon.AmmoGive1 15
    Weapon.AmmoUse1 1
    Weapon.AmmoType2 "ForceVentAmmo"
    Weapon.AmmoUse2 1
    
    Weapon.SlotNumber 5
	Weapon.UpSound "hammer/up"
    
    Inventory.PickupMessage "$CYBERRUNNER_PK_CANNON"
    Inventory.PickupSound "hammer/pickup"
    Obituary "$CYBERRUNNER_OB_CANNON"
    
    States
    {
      Spawn:
        CYCN Z 2 bright
        loop
        
      Fire:
	    TNT1 A 0 A_JumpIfInventory("WallHackVision",1,"DryFire")
        TNT1 A 0 A_JumpIfNoAmmo("DryFire")
		TNT1 A 0 A_PlayWeaponSound("hammer/ready")
		TNT1 A 15
        TNT1 A 0 A_PlaySound("hammer/fire", CHAN_WEAPON)
        TNT1 A 0 A_GunFlash
        TNT1 A 0 A_ZoomFactor(0.90)
        TNT1 A 0 A_FireCustomMissile("CannonShot1", 0, 1, -16, 0, 0, 3)
        TNT1 A 0 A_FireCustomMissile("CannonShot2", 0, 0, 16, 0, 0, 3)
        TNT1 A 5 A_ZoomFactor(0.95)
        TNT1 A 3 A_ZoomFactor(0.975)
        TNT1 A 2 A_ZoomFactor(0.99)
		TNT1 A 10 A_ZoomFactor(1)
		TNT1 A 0 A_PlaySoundEx("hammer/wind","soundslot6")
        TNT1 A 15
		TNT1 A 0 A_SetBlend("FF FF FF",0.3,10)
        TNT1 A 25 A_WeaponReady(12)
        goto Ready
    }
}

actor CannonShot1
{
    Radius 10
    Height 10

    Projectile
    Speed 100
    Damage (75)

    RenderStyle Add
	Scale 0.15
    Alpha 0.95
    Decal Scorch
    Obituary "$CYBERRUNNER_OB_CANNON"
	DamageType "CyberCannon"
	DeathSound "hammer/explode"
	-NOGRAVITY
	Gravity 0.65
	+FORCERADIUSDMG

    States
    {
      Spawn:
        CLHM ABCDEFGHIJ 1 BRIGHT A_CustomMissile("PlasmaTrail",0,0,0)
        Loop

      Death:
        FXPL A 0 A_Explode(128,196)
        FXPL A 0 A_Explode(128,196,0)
		TNT1 A 0 A_ChangeFlag("NOGRAVITY",1)
        FXPL A 0 A_Scream//A_PlaySound("hammer/explode",CHAN_BODY)
        TNT1 A 1 A_Jump(255, "DeathEffects")
        wait

      DeathEffects:
        TNT1 A 0 A_SpawnItemEx("BlueShockwaveSpawner")
        TNT1 A 0 A_SpawnItemEx("BlueExplosionSpawner")
        goto Death2

      Death2:
        FXPL ABCDEF 2 bright 
        Stop
    }
}

actor CannonShot2 : CannonShot1
{
	States
	{
      Spawn:
        CLHM KLMNOPQRST 1 BRIGHT A_CustomMissile("RedPlasmaTrail",0,0,0)
        Loop

      DeathEffects:
        TNT1 A 0 A_SpawnItemEx("RedShockwaveSpawner")
        TNT1 A 0 A_SpawnItemEx("RedExplosionSpawner")
        goto Death2
	}
}

#include "decorate/cannoneffects.dec"
