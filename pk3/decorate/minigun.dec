actor "Cyber Minigun" : Weapon 15503
{
    Weapon.Kickback 100

    Weapon.AmmoType1 "MinigunAmmo"
    Weapon.AmmoGive1 200
    Weapon.AmmoUse1 1
    Weapon.AmmoType2 "ForceVentAmmo"
    Weapon.AmmoUse2 1
    Weapon.SlotNumber 1
	Weapon.UpSound "minigun/up"

    +NOAUTOAIM
    +AMMO_OPTIONAL
    +ALT_AMMO_OPTIONAL
    +UNDROPPABLE
    +FLOATBOB
    +NOGRAVITY
    
    RenderStyle Add
    Alpha 0.95
    
    Inventory.PickupMessage "$CYBERRUNNER_PK_MINIGUN"
    Inventory.PickupSound "minigun/pickup"

    States
    {
      Spawn:
        CYMG Z 2 bright A_SpawnItemEx("CyberMinigunSparkle", random(8, 24), 0, random(16, 24), 0, 0, random(2, 5)/5.0, random(0, 360))
        loop

      Ready:
        TNT1 A 1 A_WeaponReady
        Loop

      Deselect:
        TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Lower
        TNT1 A 1 A_Lower
        Loop

      Select:
        TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Raise
        TNT1 A 1 A_Raise
        Loop

      Fire:
        TNT1 A 0 A_JumpIfNoAmmo("DryFire")
        TNT1 A 0 A_PlayWeaponSound("plasma/shot")
        TNT1 A 0 A_FireCustomMissile("PlasmaBolt",0,1,0,1)
        TNT1 A 3
        goto Ready

      DryFire:
        TNT1 A 4
        Goto Ready
		
	  AltFire:
        TNT1 A 0 A_JumpIfNoAmmo("DryFire")
        TNT1 A 0 A_PlayWeaponSound("force/shot")
        TNT1 A 0 A_FireCustomMissile("ForceVent",0,1,0,0)
        TNT1 A 30 A_WeaponReady(8)
        goto Ready
    }
}

actor MinigunAmmo: Ammo
{
    Inventory.MaxAmount 200
    Ammo.BackpackMaxAmount 200
    +INVENTORY.IGNORESKILL
}

actor CyberMinigunSparkle
{
    +NOINTERACTION
    +CLIENTSIDEONLY
    
    ReactionTime 18
    
    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 0 A_SpawnItemEx("CyberMinigunSparkle2")
        TNT1 A 1 A_Countdown
        loop
        
      Death:
        TNT1 A 0
        stop
    }
}

actor CyberMinigunSparkle2
{
    +NOINTERACTION
    +CLIENTSIDEONLY
    
    RenderStyle Add
    Alpha 0.75
    Scale 0.5
    
    States
    {
      Spawn:
        CYMG Y 1 bright A_FadeOut(0.05)
        loop
    }
}
